//for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id "com.eriwen.gradle.js" version "2.14.1"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven-publish'
			
group = theGroup
version = theVersion
sourceCompatibility = theSourceCompatibility

eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

//Declare your sources
def jsSrcDir = 'src/main/webapp/static/scripts/nest'

javascript.source {
    dev {
        js {
            srcDir jsSrcDir
            include "*.js"
            exclude "nest.js"
        }
    }
}

combineJs {
    //encoding = "UTF-8"
    source = [
              "${jsSrcDir}/license.js",
              "${jsSrcDir}/require.js",
              "${jsSrcDir}/jquery-2.2.0.js",
              "${jsSrcDir}/../material/material.js",
              "${jsSrcDir}/nestbase.js",
              "${jsSrcDir}/nestui.js"
    ]
    dest = file(jsSrcDir + '/nest.js')
}

eclipseWtpComponent.dependsOn combineJs

war {
	filter(ReplaceTokens, tokens:['version': version])
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile ('de.nikem.nest:nest:1.0') {
		exclude group: 'javax.servlet', module: 'servlet-api'
	}
	//These dependencies must be omitted for deployment in servlet container
	providedRuntime 'javax.servlet:javax.servlet-api:+'
	providedRuntime 'javax.servlet.jsp:javax.servlet.jsp-api:+'
	providedRuntime 'javax.websocket:javax.websocket-api:+'
	testCompile 'junit:junit:4.+'
}

test {
	systemProperties 'property': 'value'
}

[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

eclipseJdt << {
	File f = file('.settings/org.eclipse.core.resources.prefs')
	f.write('eclipse.preferences.version=1\n')
	f.append('encoding/<project>=UTF-8')
}

publishing {
	publications {
        mavenJava(MavenPublication) {
        	groupId group
        
            from components.java
        }
    }
    repositories {
        maven {
            url "https://TEAMCITY:8081/artifactory/ext-release-local"
            credentials {
            	username "xxx"
            	password "xxx"
        	}
        }
    }
}